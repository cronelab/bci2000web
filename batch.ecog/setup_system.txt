# Set Defaults
Set SubjectID UnknownPatient
Set Blackrock 0
Set SampleGroup 2
Set OnlineFunctionalMapping 0

# Acquire System Configuration.  If batch.ecog/config.txt doesn't exist, we'll interactively configure.
Set configfile ../batch.ecog/config.txt
IF [ ${IS FILE $configfile} ]
	
	Set SubjectID ${EXECUTE SCRIPT $configfile SubjectID}
	Set Blackrock ${EXECUTE SCRIPT $configfile Blackrock}
	Set SampleGroup ${EXECUTE SCRIPT $configfile SampleGroup}
	Set OnlineFunctionalMapping ${EXECUTE SCRIPT $configfile OnlineFunctionalMapping}
	Echo Using config.txt for Subject $SubjectID ...

ELSE

	Echo Please enter a subject ID:
	Set SubjectID ${Read Line}

	# Set up Blackrock if necessary
	Echo Number of Blackrock Instances? (0 = Use SignalGenerator)
	Set Blackrock ${Read Line}

	# Determine System Sampling Rate
	Echo Set Sampling Rate? 0:Default, 1: 500, 2:1000, 3:2000, 4:10000, 5:30000
	Set SampleGroup ${Read Line}

	# Determine if OFM is to be run
	Echo Perform Realtime Feature Extraction? (1/0)
	Set OnlineFunctionalMapping ${Read Line}
	
END

IF [ $1 == Mapping ];
	Set OnlineFunctionalMapping 1
END

# If we're not using Blackrock, use the SignalGenerator
IF [ $Blackrock != 0 ];
	Start executable Blackrock --local --LogKeyboard=1 --LogMouse=1 --LogJoystick=1
ELSE
	Start executable SignalGenerator --local --LogKeyboard=1 --LogMouse=1 --LogJoystick=1
END
Sleep 1

# Determine SignalProcessing module to use
IF [ $1 == Recording ];
	Start executable DummySignalProcessing --local
ELSEIF [ $1 == CCEPS ];
	Start executable CCEPSignalProcessing --local
ELSE
	IF [ $OnlineFunctionalMapping != 0 ];
		Start executable BandPowerSignalProcessing --local
	ELSE
		Start executable DummySignalProcessing --local
	END
END
Sleep 1

# We can only set parameters once the system has been set up
Wait for Connected

IF [ $1 != JoystickTask ];
	Set parameter LogJoystick 0
END

# Take care of Source Parameters
IF [ $Blackrock == 0 ];
	Set parameter SourceCh 200
ELSE
	Set parameter NSPInstances $Blackrock
END

# Handle SamplingRate/SampleBlockSize adjustment
IF [ $SampleGroup == 1 ]; Set parameter SamplingRate 500; END
IF [ $SampleGroup == 1 ] && [ $Blackrock == 0 ]; Set parameter SampleBlockSize 25; END

IF [ $SampleGroup == 2 ]; Set parameter SamplingRate 1000; END
IF [ $SampleGroup == 2 ] && [ $Blackrock == 0 ]; Set parameter SampleBlockSize 50; END

IF [ $SampleGroup == 3 ]; Set parameter SamplingRate 2000; END
IF [ $SampleGroup == 3 ] && [ $Blackrock == 0 ]; Set parameter SampleBlockSize 100; END

IF [ $SampleGroup == 4 ]; Set parameter SamplingRate 10000; END
IF [ $SampleGroup == 4 ] && [ $Blackrock == 0 ]; Set parameter SampleBlockSize 500; END

IF [ $SampleGroup == 5 ]; Set parameter SamplingRate 30000; END
IF [ $SampleGroup == 5 ] && [ $Blackrock == 0 ]; Set parameter SampleBlockSize 1500; END


IF [ $OnlineFunctionalMapping != 0 ] && [ $1 != Recording ] && [ $1 != CCEPS ];
	Load parameterfile "../parms.ecog/HGSigProc.prm"
END

Set Parameter ConnectorOutputAddress localhost:20321

Load parameterfile "../batch.ecog/screen_setup.prm"

Set parameter SubjectName $SubjectID